C
      SUBROUTINE PCOEX(TR, PR, RHOL, RHOV, IWANT, PROPR, IERR)
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
C                     *** PCOEX ***                                    C
C THIS ROUTINE COMPUTES THE REDUCED COEXISTENCE PRESSURE P/PC AT A     C
C GIVEN REDUCED TEMPERATURE TR.  IT ALSO RETURNS THE COEXISTING        C
C DENSITIES.  THE STRATEGY IS TO GET A GOOD INITIAL GUESS AND THEN     C
C CONVERGE THE LN OF THE K-FACTOR TO ZERO.                             C
C                                                                      C
C ARGUMENT LIST:                                                       C
C  NAME  TYPE I/O? EXPLANATION                                         C
C ------ ---- ---- --------------------------------------------------  C
C TR      R    I   REDUCED TEMPERATURE, T/TC                           C
C PR      R    O   REDUCED COEXISTENCE PRESSURE, P/PC                  C
C RHOL    R    O   REDUCED SAT. LIQUID DENSITY, RHOL/RHOC              C
C RHOV    R    O   REDUCED SAT. VAPOR DENSITY, RHOV/RHOC               C
C IWANT   IA   -   PROPERTY REQUEST VECTOR FOR USE IN PROPS2 ROUTINE   C
C PROPR   RA   O   VECTOR OF REDUCED PROPERTIES AS REQUESTED BY IWANT  C
C                  TO BE RETURNED BY THE PROP2 ROUTINE                 C
C IERR    I    O   ERROR FLAG                                          C
C                  0 = SUCCESS                                         C
C                  1 = TEMPERATURE OUT OF RANGE                        C
C                  2 = UNABLE TO BRACKET (PROBABLY BECAUSE VERY CLOSE  C
C                      TO TC) RETURN VALUES FROM SATURATION EQUATIONS  C
C                  3 = UNABLE TO CONVERGE BOUNDED SOLUTION.  RETURN    C
C                      LAST GUESS FOR SOLUTION                         C
C                  4 = TEMPERATURE BELOW TRIPLE POINT BUT IN RANGE     C
C                                                                      C
C MAINTENANCE:                                                         C
C                                                                      C
C 21JUL95 - INITIAL CREATION BY AHH                                    C
C 31AUG95 - HANDLE STEP WHERE CAN'T FIND DENSITY ROOT IN PHASE         C
C 01SEP95 - DON'T TRY TO ITERATE FOR TR > .99999                       C
C 01SEP95 - FORCE AT LEAST A 1% STEP IN BOUNDED SECANT SEARCH          C
C 06SEP95 - USE TR INSTEAD OF TAU IN DFIND ARGUMENT LIST               C
C 06SEP95 - AHH: IMPLEMENT IERR=2 FOR POINTS TOO CLOSE TO TC           C
C 28SEP95 - AHH: PARAMETERIZE NUMBER OF PROPERTIES                     C
C 23MAY96 - AHH: RETURN BETTER NEAR-CRITICAL DENSITIES FOR IERR=2      C
C 30AUG96 - AHH: LOWERCASE INCLUDES FOR OTHER PLATFORMS                C
C 25SEP96 - AHH: SET RHOL AND RHOV FOR IERR=1 TO AVOID UNSET VARS      C
C 09JAN97 - AHH: ALLOW CALCULATIONS BELOW TRIPLE POINT                 C
C 11FEB97 - AHH: MAKE EXP'S UNIFORMLY DOUBLE PRECISION                 C
C 21MAY97 - AHH: USE COMMONS FOR WATER PARAMETERS                      C
C 10NOV99 - AHH: GIVE IT BETTER GUESSES IF NEAR CRITICAL POINT         C
C                                                                      C
CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INCLUDE 'nprop.cmn'
      INCLUDE 'wconst.cmn'
      INCLUDE 'wlimit.cmn'
      PARAMETER (TOL=1.D-7)
      PARAMETER (TTRIP = TTRIPW/TCW, TMIN = TSMIN/TCW)
      LOGICAL LITER1
      DIMENSION PROPR(NPROP)
      DIMENSION IWANT(NPROP)
      LITER1 = .TRUE.
C
C TEMPERATURE RANGE CHECK
C
      IF (TR .LT. TMIN) THEN
        IERR = 1
        PR = 1.D-8
        RHOL = 0.D0
        RHOV = 0.D0
        RETURN
      ELSE IF (TR .GE. 1.D0) THEN
        IERR = 1
        PR = 1.D0
        RHOL = 1.D0
        RHOV = 1.D0
        RETURN
      ENDIF
C
C GET INITIAL GUESSES FOR PSAT, RHOL, RHOV
C
      CALL PVAP(TR, P0, DP0)
      IF (TR .GT. 0.999998D0) THEN
        RHOL0 = DLSATC(TR)
        RHOV0 = DVSATC(TR)
        CALL PVAPC(TR, P0, DP0)
      ELSE
        RHOL0 = DLSAT(TR)
        RHOV0 = DVSAT(TR)
      ENDIF
C
C FIND LIQUID AND VAPOR DENSITIES AT INITIALLY GUESSED PRESSURE
C
      CALL DFIND(RHOL,P0,RHOL0,TR,DPD,IWANT,PROPR,IER1)
      CALL DFIND(RHOV,P0,RHOV0,TR,DPD,IWANT,PROPR,IER2)
C
C IF YOU COULDN'T FIND DENSITY ROOTS AT THE INITIAL GUESS, YOU ARE
C PROBABLY TOO CLOSE TO TC.  RETURN INIT. GUESS VALUES
C
      IF ((IER1.NE.0) .OR. (IER2.NE.0)) GO TO 666
C
C GET THE K-FACTOR AT THIS TEMP, RHOL, AND RHOV
C
      CALL KFACT(TR,RHOL,RHOV,FLIQ,FVAP,XK,IWANT,PROPR)
C
C OBJECTIVE FUNCTION IS NEGATIVE OF LN OF THE K-FACTOR
C
      F0 = -LOG(XK)
C
C NOW SEARCH FOR A PRESSURE ON THE OTHER SIDE OF EQUILIBRIUM
C IF F0 IS NEGATIVE, THE PRESSURE NEEDS TO BE HIGHER
C IF YOU JUMP PAST THE 2-PHASE PART OF THE ISOTHERM, HALVE THE STEP
C
      POLD = P0
      IF (F0 .LT. 0.D0) THEN
        PINCR = .01*(1.D0-P0)
        DO 100 I=1,100
  110     PNEW = POLD + PINCR
          CALL DFIND(RHOL,PNEW,RHOL0,TR,DPD,IWANT,PROPR,IER1)
          CALL DFIND(RHOV,PNEW,RHOV0,TR,DPD,IWANT,PROPR,IER2)
          IF ((IER1.NE.0) .OR. (IER2.NE.0)) THEN
            PINCR = 0.5*PINCR
            GO TO 110
          ENDIF
          CALL KFACT(TR,RHOL,RHOV,FLIQ,FVAP,XK,IWANT,PROPR)
          F1 = -LOG(XK)
          IF (F1 .GT. 0.D0) THEN
            PLOW = POLD
            PHIGH = PNEW      
            FLOW = F0
            FHIGH = F1
            GO TO 201
          ELSE
            POLD = PNEW
            F0 = F1
          ENDIF
  100   CONTINUE
C
C - IF YOU GET HERE, YOU WERE UNABLE TO BRACKET THE PRESSURE
C
        GO TO 666
C
      ELSE
C
C IF F WAS TOO HIGH, NEED TO LOWER THE PRESSURE
C
        PINCR = .01*P0
        DO 200 I=1,90
  210     PNEW = POLD - PINCR
          CALL DFIND(RHOL,PNEW,RHOL0,TR,DPD,IWANT,PROPR,IER1)
          CALL DFIND(RHOV,PNEW,RHOV0,TR,DPD,IWANT,PROPR,IER2)
          IF ((IER1.NE.0) .OR. (IER2.NE.0)) THEN
            PINCR = 0.5*PINCR
            GO TO 210
          ENDIF
          CALL KFACT(TR,RHOL,RHOV,FLIQ,FVAP,XK,IWANT,PROPR)
          F1 = -LOG(XK)
          IF (F1 .LT. 0.D0) THEN
            PLOW = PNEW
            PHIGH = POLD      
            FLOW = F1
            FHIGH = F0
            GO TO 201
          ELSE
            POLD = PNEW
            F0 = F1
          ENDIF
  200   CONTINUE
C
C - IF YOU GET HERE, YOU WERE UNABLE TO BRACKET THE PRESSURE
C
        GO TO 666
C
      ENDIF
C
  201 CONTINUE
C
C NOW THE VAPOR PRESSURE IS BRACKETED BY PLOW AND PHIGH, WITH
C CORRESPONDING OBJECTIVE FUNCTION VALUES OF FLOW AND FHIGH.
C CONVERGE ON THE SOLUTION BY SECANT METHOD ON LN P
C
      PLNLOW = LOG(PLOW)
      PLNHI  = LOG(PHIGH)
      DO 300 I=1,100
        FRDIST = -FLOW / (FHIGH-FLOW)
        IF (FRDIST .LT. 0.01) THEN
          LITER1 = .TRUE.
          FRDIST = 0.01
        ELSE IF (FRDIST .GT. 0.99) THEN
          LITER1 = .TRUE.
          FRDIST = 0.99
        ENDIF
        PLNNEW = PLNLOW + FRDIST*(PLNHI-PLNLOW)
        PNEW = DEXP(PLNNEW)        
        CALL DFIND(RHOL,PNEW,RHOL0,TR,DPD,IWANT,PROPR,IER1)
        CALL DFIND(RHOV,PNEW,RHOV0,TR,DPD,IWANT,PROPR,IER2)
        CALL KFACT(TR,RHOL,RHOV,FLIQ,FVAP,XK,IWANT,PROPR)
        FNEW = -LOG(XK)
C
C CHECK FOR CONVERGENCE
C BUT NOT IF IT IS THE FIRST TIME THROUGH OR YOU HAVE BOUNDED THE STEP
C
        IF (LITER1) THEN
          LITER1 = .FALSE.
        ELSE
          IF (DABS(FNEW) .LT. TOL) GO TO 777
        ENDIF
C
C MAKE NEW BOUNDS DEPENDING ON WHETHER NEW GUESS WAS HIGH OR LOW
C
        IF (FNEW .GT. 0.D0) THEN
          FHIGH = FNEW
          PHIGH = PNEW
          PLNHI = PLNNEW
        ELSE
          FLOW = FNEW
          PLOW = PNEW
          PLNLOW = PLNNEW
        ENDIF
  300 CONTINUE
C
C IF YOU GET TO HERE, YOU DID NOT CONVERGE ON THE BOUNDED ROOT
C
      IERR = 3
      PR = PNEW
      RETURN
C
  666 CONTINUE
C
C GET HERE FOR INITIAL FAILURE OR FAILURE TO BRACKET SOLUTION
C RETURN INITIAL GUESSES
C
      IERR = 2
      PR = P0
      RHOL = RHOL0
      RHOV = RHOV0
      RETURN
C
  777 CONTINUE
C
C CONVERGENCE
C
      IERR = 0
C
C NOTE EXTRAPOLATION BELOW TRIPLE POINT
C
      IF (TR .LT. TTRIP) IERR = 4
C  
      PR = PNEW
      RETURN
      END
